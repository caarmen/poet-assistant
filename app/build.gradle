/*
 * Copyright (c) 2016-2018 Carmen Alvarez
 *
 * This file is part of Poet Assistant.
 *
 * Poet Assistant is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Poet Assistant is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Poet Assistant.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'jacoco'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }
    jacoco {
        version "0.8.2"
    }
    lintOptions {
        abortOnError true
        textReport true
        ignoreWarnings true
        disable 'RestrictedApi'  // https://stackoverflow.com/questions/45648530/restricted-api-lint-error-when-deleting-table-room-persistence
    }

    defaultConfig {
        applicationId "ca.rmen.android.poetassistant"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 112701
        versionName "1.27.1"
        // setting vectorDrawables.useSupportLibrary = true means pngs won't be generated at
        // build time: http://android-developers.blogspot.fr/2016/02/android-support-library-232.html
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        // used by Room, to test migrations
        sourceSets {
            main.java.srcDirs += "$projectDir/src/main/kotlin"
            androidTest.assets.srcDirs +=
                    files("$projectDir/src/androidTest/schemas".toString())
            test {
                manifest.srcFile "src/test/AndroidManifest.xml"
                java.srcDirs += "$projectDir/src/test/kotlin"
            }
            androidTest.java.srcDirs += "$projectDir/src/androidTest/kotlin"
        }

        // used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
            applicationIdSuffix = ".test"
            resValue "string", "search_provider_authority", android.defaultConfig.applicationId + applicationIdSuffix + ".SuggestionsProvider"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "search_provider_authority", android.defaultConfig.applicationId + ".SuggestionsProvider"
        }
    }

    testOptions {
        unitTests {
            all {
                jvmArgs '-noverify', '-ea'
                jacoco {
                    includeNoLocationClasses = true
                }
            }
            includeAndroidResources = true
        }
    }

    if (rootProject.hasProperty("AndroidSigningKeyAlias")
            && rootProject.hasProperty("AndroidSigningKeyPassword")
            && rootProject.hasProperty("AndroidSigningStoreFile")
            && rootProject.hasProperty("AndroidSigningStorePassword")) {
        println "Using signing properties from gradle properties"
        signingConfigs {
            release {
                keyAlias rootProject.AndroidSigningKeyAlias
                keyPassword rootProject.AndroidSigningKeyPassword
                storeFile file(rootProject.AndroidSigningStoreFile)
                storePassword rootProject.AndroidSigningStorePassword
            }
            android.buildTypes.release.signingConfig = signingConfigs.release
        }
    } else if (System.env.AndroidSigningKeyPassword != null
                && System.env.AndroidSigningKeyPassword != null
                && System.env.AndroidSigningStoreFile != null
                && System.env.AndroidSigningStorePassword != null) {
        println "Using signing properties from environment variables"
            signingConfigs {
                release {
                    keyAlias System.env.AndroidSigningKeyAlias
                    keyPassword System.env.AndroidSigningKeyPassword
                    storeFile file(System.env.AndroidSigningStoreFile)
                    storePassword System.env.AndroidSigningStorePassword
                }
                android.buildTypes.release.signingConfig = signingConfigs.release
            }
    } else {
        println "No signing properties found"
        android.buildTypes.release.signingConfig = null
    }
}

jacoco {
    toolVersion '0.8.2'
}
android.applicationVariants.all { variant ->
    variant.mergeAssets.doLast {
        copy {
            from project.rootDir
            into "${project.buildDir}/intermediates/merged_assets/${variant.buildType.name}/merge${variant.buildType.name.capitalize()}Assets/out/"
            include 'LICENSE.txt'
            include 'LICENSE-rhyming-dictionary.txt'
            include 'LICENSE-thesaurus-wordnet.txt'
            include 'LICENSE-dictionary-wordnet.txt'
            include 'LICENSE-google-ngram-dataset.txt'
        }
    }
}

project.gradle.taskGraph.whenReady {
    connectedDebugAndroidTest {
        ignoreFailures = true
    }
}

repositories {
    google()
    jcenter()
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.4'
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'com.google.android.material:material:1.1.0-alpha02'
    implementation 'androidx.preference:preference:1.1.0-alpha02'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'ca.rmen:rhymer:1.2.0'
    implementation 'ca.rmen:porter-stemmer:1.0.0'
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0-alpha01'
    implementation 'androidx.room:room-runtime:2.1.0-alpha03'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    kapt 'androidx.room:room-compiler:2.1.0-alpha03'
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    androidTestImplementation 'androidx.room:room-testing:2.1.0-alpha03'
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.1"
    kaptTest "com.google.dagger:dagger-compiler:$dagger_version"

    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
    androidTestImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"

    androidTestImplementation group: 'org.easytesting', name: 'fest-reflect', version: '1.4.1'

    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
}

// Only show real releases with the ben-manes plugin.
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'alpha-preview', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if ("com.android.databinding" == selection.candidate.group) {
                rejected = true
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
task jacocoTestReport(type: JacocoReport, dependsOn: ["testDebugUnitTest", "createDebugCoverageReport"]) {
    reports {
        html.enabled = true
    }
    classDirectories = fileTree(
            dir: "${buildDir}",
            includes: ["tmp/kotlin-classes/debug/ca/rmen/android/poetassistant/**/*.class",
                       "intermediates/javac/debug/compileDebugWithJavac/classes/ca/rmen/android/poetassistant/**/*.class"],
            excludes: ["**/R.class", "**/R*.class", "**/Manifest.class", "**/Manifest*.class", "**/BuildConfig.class",
                       // ignore databinding generated code:
                       "**/ca/rmen/android/poetassistant/databinding/*.class",
                       "**/ca/rmen/android/poetassistant/BR.class",
                       "**/ca/rmen/android/poetassistant/DataBinderMapperImpl.class",
                       "**/ca/rmen/android/poetassistant/DataBinderMapperImpl\$*.class",
                       "**/*_Impl*.class",
                       // ignore dagger generated code:
                       "**/ca/rmen/android/poetassistant/**/DaggerAppComponent*.class",
                       "**/ca/rmen/android/poetassistant/**/*_Factory.class",
                       "**/ca/rmen/android/poetassistant/**/*_Provides*.class",
                       "**/ca/rmen/android/poetassistant/**/*Injector.class",
                       // ignore generated code not in our package
                       "**/android/databinding/*.class",
                       "**/android/databinding/**/*.class",
                       "**/com/android/**/*.class"])
    sourceDirectories = files("${project.projectDir}/src/main/java", "${project.projectDir}/src/main/kotlin")
    executionData = fileTree(
            dir: "${buildDir}",
            includes: [
                    "jacoco/testDebugUnitTest.exec",
                    "outputs/code-coverage/connected/*coverage.ec"
            ])
}

// Necessary for Prefs to work (see https://kotlinlang.org/docs/reference/kapt.html)
kapt {
    correctErrorTypes = true
    javacOptions {
        // Increase the max count of errors from annotation processors.
        // Default is 100.
        option("-Xmaxerrs", 10000)
    }
}
