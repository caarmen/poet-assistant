/*
 * Copyright (c) 2016-2017 Carmen Alvarez
 *
 * This file is part of Poet Assistant.
 *
 * Poet Assistant is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Poet Assistant is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Poet Assistant.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'jacoco'



android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError true
        textReport true
        ignoreWarnings true
        disable 'RestrictedApi'  // https://stackoverflow.com/questions/45648530/restricted-api-lint-error-when-deleting-table-room-persistence
    }

    defaultConfig {
        applicationId "ca.rmen.android.poetassistant"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 112202
        versionName "1.22.2"
        // setting vectorDrawables.useSupportLibrary = true means pngs won't be generated at
        // build time: http://android-developers.blogspot.fr/2016/02/android-support-library-232.html
        vectorDrawables.useSupportLibrary = true
        jackOptions {
            enabled false // too slow! :(
        }
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        // used by Room, to test migrations
        sourceSets {
            androidTest.assets.srcDirs +=
                    files("$projectDir/src/androidTest/schemas".toString())
        }

        // used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
            applicationIdSuffix = ".test"
            resValue "string", "search_provider_authority", android.defaultConfig.applicationId + applicationIdSuffix + ".SuggestionsProvider";
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "search_provider_authority", android.defaultConfig.applicationId + ".SuggestionsProvider";
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    if (rootProject.hasProperty("AndroidSigningKeyAlias")
            && rootProject.hasProperty("AndroidSigningKeyPassword")
            && rootProject.hasProperty("AndroidSigningStoreFile")
            && rootProject.hasProperty("AndroidSigningStorePassword")) {
        println "Using signing properties from gradle properties"
        signingConfigs {
            release {
                keyAlias rootProject.AndroidSigningKeyAlias
                keyPassword rootProject.AndroidSigningKeyPassword
                storeFile file(rootProject.AndroidSigningStoreFile)
                storePassword rootProject.AndroidSigningStorePassword
            }
            android.buildTypes.release.signingConfig = signingConfigs.release
        }
    } else if (System.env.AndroidSigningKeyPassword != null
                && System.env.AndroidSigningKeyPassword != null
                && System.env.AndroidSigningStoreFile != null
                && System.env.AndroidSigningStorePassword != null) {
        println "Using signing properties from environment variables"
            signingConfigs {
                release {
                    keyAlias System.env.AndroidSigningKeyAlias
                    keyPassword System.env.AndroidSigningKeyPassword
                    storeFile file(System.env.AndroidSigningStoreFile)
                    storePassword System.env.AndroidSigningStorePassword
                }
                android.buildTypes.release.signingConfig = signingConfigs.release
            }
    } else {
        println "No signing properties found"
        android.buildTypes.release.signingConfig = null
    }
}

android.applicationVariants.all { variant ->
    variant.mergeAssets.doLast {
        copy {
            from project.rootDir
            into "${project.buildDir}/intermediates/assets/${variant.buildType.name}"
            include 'LICENSE.txt'
            include 'LICENSE-rhyming-dictionary.txt'
            include 'LICENSE-thesaurus-wordnet.txt'
            include 'LICENSE-dictionary-wordnet.txt'
            include 'LICENSE-google-ngram-dataset.txt'
        }
    }
}

repositories {
    jcenter();
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.1'
        classpath 'me.tatarka:gradle-retrolambda:3.7.0'
    }
}

dependencies {
    annotationProcessor 'org.jraf:prefs-compiler:1.2.2'

    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:preference-v7:26.1.0'
    compile 'com.android.support:preference-v14:26.1.0'
    compile 'ca.rmen:rhymer:1.2.0'
    compile 'ca.rmen:porter-stemmer:1.0.0'
    compile 'org.jraf:prefs:1.2.2'
    compile 'com.google.dagger:dagger:2.12'
    compile "io.reactivex.rxjava2:rxjava:2.1.5"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'android.arch.lifecycle:extensions:1.0.0-rc1'
    compile 'android.arch.persistence.room:runtime:1.0.0-rc1'
    annotationProcessor 'android.arch.persistence.room:compiler:1.0.0-rc1'
    androidTestCompile 'android.arch.persistence.room:testing:1.0.0-rc1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.12'
    androidTestAnnotationProcessor 'com.google.dagger:dagger-compiler:2.12'

    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.5"


    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    androidTestCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    androidTestCompile group: 'org.easytesting', name: 'fest-reflect', version: '1.4.1'

    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
}

// Only show real releases with the ben-manes plugin.
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'alpha-preview', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if ("com.android.databinding" == selection.candidate.group) {
                rejected = true
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
task jacocoTestReport(type: JacocoReport, dependsOn: ["testDebugUnitTest", "createDebugCoverageReport"]) {
    reports {
        html.enabled = true
    }
    classDirectories = fileTree(
            dir: "${buildDir}/intermediates/classes/debug",
            excludes: ["**/R.class", "**/R*.class", "**/Manifest.class", "**/Manifest*.class", "**/BuildConfig.class",
                       // ignore databinding generated code:
                       "ca/rmen/android/poetassistant/databinding/*.class",
                       "ca/rmen/android/poetassistant/BR.class",
                       // ignore dagger generated code:
                       "ca/rmen/android/poetassistant/settings/SettingsEditorWrapper.class",
                       "ca/rmen/android/poetassistant/settings/SettingsPrefs.class",
                       "ca/rmen/android/poetassistant/**/DaggerAppComponent*.class",
                       "ca/rmen/android/poetassistant/**/*_Factory.class",
                       "ca/rmen/android/poetassistant/**/*_Provides*.class",
                       "ca/rmen/android/poetassistant/**/*Injector.class",
                       // ignore generated code not in our package
                       "android/databinding/**/*.class",
                       "com/android/**/*.class"])
    sourceDirectories = files("${project.projectDir}/src/main/java")
    executionData = fileTree(
            dir: "${buildDir}",
            includes: [
                    "jacoco/testDebugUnitTest.exec",
                    "outputs/code-coverage/connected/*coverage.ec"
            ])
}
