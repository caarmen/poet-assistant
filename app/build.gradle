/*
 * Copyright (c) 2016-2018 Carmen Alvarez
 *
 * This file is part of Poet Assistant.
 *
 * Poet Assistant is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Poet Assistant is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Poet Assistant.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'jacoco'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
android {
    compileSdkVersion 29
    buildToolsVersion "28.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }
    jacoco {
        version "0.8.4"
    }
    lintOptions {
        abortOnError true
        textReport true
        ignoreWarnings true
        disable 'RestrictedApi'  // https://stackoverflow.com/questions/45648530/restricted-api-lint-error-when-deleting-table-room-persistence
    }

    defaultConfig {
        applicationId "ca.rmen.android.poetassistant"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 113001
        versionName "1.30.1"
        // setting vectorDrawables.useSupportLibrary = true means pngs won't be generated at
        // build time: http://android-developers.blogspot.fr/2016/02/android-support-library-232.html
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        // used by Room, to test migrations
        sourceSets {
            main.java.srcDirs += "$projectDir/src/main/kotlin"
            main.assets.srcDirs += "${project.buildDir}/generated/license_assets"
            androidTest.assets.srcDirs +=
                    files("$projectDir/src/androidTest/schemas".toString())
            test {
                manifest.srcFile "src/test/AndroidManifest.xml"
                java.srcDirs += "$projectDir/src/test/kotlin"
            }
            androidTest.java.srcDirs += "$projectDir/src/androidTest/kotlin"
        }

        // used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
            applicationIdSuffix = ".test"
            resValue "string", "search_provider_authority", android.defaultConfig.applicationId + applicationIdSuffix + ".SuggestionsProvider"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "search_provider_authority", android.defaultConfig.applicationId + ".SuggestionsProvider"
        }
    }

    testOptions {
        unitTests {
            all {
                jvmArgs '-noverify', '-ea'
                jacoco {
                    includeNoLocationClasses = true
                }
            }
            includeAndroidResources = true
        }
    }

    if (rootProject.hasProperty("AndroidSigningKeyAlias")
            && rootProject.hasProperty("AndroidSigningKeyPassword")
            && rootProject.hasProperty("AndroidSigningStoreFile")
            && rootProject.hasProperty("AndroidSigningStorePassword")) {
        println "Using signing properties from gradle properties"
        signingConfigs {
            release {
                keyAlias rootProject.AndroidSigningKeyAlias
                keyPassword rootProject.AndroidSigningKeyPassword
                storeFile file(rootProject.AndroidSigningStoreFile)
                storePassword rootProject.AndroidSigningStorePassword
            }
            android.buildTypes.release.signingConfig = signingConfigs.release
        }
    } else if (System.env.AndroidSigningKeyPassword != null
                && System.env.AndroidSigningKeyPassword != null
                && System.env.AndroidSigningStoreFile != null
                && System.env.AndroidSigningStorePassword != null) {
        println "Using signing properties from environment variables"
            signingConfigs {
                release {
                    keyAlias System.env.AndroidSigningKeyAlias
                    keyPassword System.env.AndroidSigningKeyPassword
                    storeFile file(System.env.AndroidSigningStoreFile)
                    storePassword System.env.AndroidSigningStorePassword
                }
                android.buildTypes.release.signingConfig = signingConfigs.release
            }
    } else {
        println "No signing properties found"
        android.buildTypes.release.signingConfig = null
    }
}

jacoco {
    toolVersion '0.8.4'
}
android.applicationVariants.all { variant ->
    variant.mergeAssets.doLast {
        copy {
            from project.rootDir
            into "${project.buildDir}/generated/license_assets/"
            include 'LICENSE.txt'
            include 'LICENSE-rhyming-dictionary.txt'
            include 'LICENSE-thesaurus-wordnet.txt'
            include 'LICENSE-dictionary-wordnet.txt'
            include 'LICENSE-google-ngram-dataset.txt'
        }
    }
}

project.gradle.taskGraph.whenReady {
    connectedDebugAndroidTest {
        ignoreFailures = true
    }
}

repositories {
    google()
    jcenter()
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.27.0'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6'
    }
}

dependencies {

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.preference:preference:$preference_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "ca.rmen:rhymer:$rhymer_version"
    implementation "ca.rmen:porter-stemmer:$porterstemmer_version"
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    // We need to explicitly add a couple of api dependencies here, otherwise alpha versions
    // of these libs will be pulled in transitively (by a non-alpha databinding dependency...)
    api "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    api "androidx.collection:collection:$collection_version"

    kapt "androidx.room:room-compiler:$room_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    androidTestImplementation "androidx.room:room-testing:$room_version"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"

    testImplementation "junit:junit:$junit_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    kaptTest "com.google.dagger:dagger-compiler:$dagger_version"

    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
    androidTestImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"

    androidTestImplementation group: "org.easytesting", name: "fest-reflect", version: "$festreflect_version"

    androidTestImplementation "androidx.test:runner:$androidx_test_runner_version"
    androidTestImplementation "androidx.test:rules:$androidx_test_rules_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "androidx.test:core:$androidx_test_core_version"
    androidTestImplementation "androidx.test.ext:junit:$androidx_junit_version"
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'


}

// Only show real releases with the ben-manes plugin.
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'alpha-preview', 'beta', 'rc', 'cr', 'm', 'eap'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if ("com.android.databinding" == selection.candidate.group) {
                rejected = true
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
task jacocoTestReport(type: JacocoReport, dependsOn: ["testDebugUnitTest", "createDebugCoverageReport"]) {
    reports {
        html.enabled = true
    }
    classDirectories = fileTree(
            dir: "${buildDir}",
            includes: ["tmp/kotlin-classes/debug/ca/rmen/android/poetassistant/**/*.class",
                       "intermediates/javac/debug/compileDebugWithJavac/classes/ca/rmen/android/poetassistant/**/*.class"],
            excludes: ["**/R.class", "**/R*.class", "**/Manifest.class", "**/Manifest*.class", "**/BuildConfig.class",
                       // ignore databinding generated code:
                       "**/ca/rmen/android/poetassistant/databinding/*.class",
                       "**/ca/rmen/android/poetassistant/BR.class",
                       "**/ca/rmen/android/poetassistant/DataBinderMapperImpl.class",
                       "**/ca/rmen/android/poetassistant/DataBinderMapperImpl\$*.class",
                       "**/*_Impl*.class",
                       // ignore dagger generated code:
                       "**/ca/rmen/android/poetassistant/**/DaggerAppComponent*.class",
                       "**/ca/rmen/android/poetassistant/**/*_Factory.class",
                       "**/ca/rmen/android/poetassistant/**/*_Provides*.class",
                       "**/ca/rmen/android/poetassistant/**/*Injector.class",
                       // ignore generated code not in our package
                       "**/android/databinding/*.class",
                       "**/android/databinding/**/*.class",
                       "**/com/android/**/*.class"])
    sourceDirectories = files("${project.projectDir}/src/main/java", "${project.projectDir}/src/main/kotlin")
    executionData = fileTree(
            dir: "${buildDir}",
            includes: [
                    "jacoco/testDebugUnitTest.exec",
                    "outputs/code_coverage/debugAndroidTest/connected/*coverage.ec"
            ])
}

// Necessary for Prefs to work (see https://kotlinlang.org/docs/reference/kapt.html)
kapt {
    correctErrorTypes = true
    javacOptions {
        // Increase the max count of errors from annotation processors.
        // Default is 100.
        option("-Xmaxerrs", 10000)
    }
}

